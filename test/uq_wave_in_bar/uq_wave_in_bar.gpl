#!/usr/bin/env gnuplot
set macros
data="u.dat"
! ./postprocess.sh
pdf="term pdf color lw 2 22 size 4in,9in"
png="term png size 400,900"

set style line 1 lt 1 lw 1 ps 0.75 lc rgbcolor "black"
set style line 2 lt 1 lw 3         lc rgbcolor "red"

set xlabel "X-COORDINATE"
do for [idx=1:4] {
set ylabel "DISPLACEMENT"
plot \
data u 1:2 index idx w l  ls 2 t "analytic",\
data u 4:5 index idx w lp ls 1 t "FE" 
pause -1 "> displacement ".idx

set ylabel "VELOCITY"
plot \
data u 1:3 index idx w l  ls 2 t "analytic",\
data u 4:6 index idx w lp ls 1 t "FE" 
pause -1 "> velocity ".idx
}

s = 1e3
file='"wave_displacement_comparision.png"'
set @png ; set output @file
set multiplot layout 3,1
plot \
data u 1:(($2)*s):-2 index 0:102:8 w l t "analytic" lc palette lw 3
plot \
data u 4:(($5)*s):-2 index 0:102:8 w lp t "FE" lc palette lw 3
plot \
data u 4:((($5)-($2))*s):-2 index 0:102:8 w l t "FE-analytic" lc palette lw 3
unset multiplot

s = 1e-3
file='"wave_velocity_comparision.png"'
set @png ; set output @file
set multiplot layout 3,1
plot \
data u 1:(($3)*s):-2 index 0:102:8 w l t "analytic" lc palette lw 3
plot \
data u 4:(($6)*s):-2 index 0:102:8 w l t "FE" lc palette lw 3
plot \
data u 4:((($6)-($3))*s):-2 index 0:102:8 w l t "FE-analytic" lc palette lw 3
unset multiplot

